РИГ = Описания Граф.
Описания = ВнешниеCсылки ЛокальныеКонстанты.
ВнешниеCсылки = "External" endl {/ {sep} Положительное sep Имя endl /}.
ЛокальныеКонстанты = "Local" endl {/ {sep} Положительное sep Значение endl /}.
sep = {/ tab | space /}.
Значение = Целое | Действительное | Задержанное | Элементарное | Имя.
Имя = id [ "." id ].
id = ( буква | "_" ) { буква | цифра | "_" }.
Граф = Заголовок СписокВершин.
Заголовок = "id" sep "delay" sep "operation" sep "links" sep "positions" endl.
СписокВершин = НачальнаяВершина {Вершина} КонечнаяВершина.
НачальнаяВершина = "0" sep "0" sep "arg" sep Позиции endl.
Вершина = НомерВершины sep НомерЗадержанногоСписка sep Оператор sep Позиции endl.
КонечнаяВершина = НомерВершины sep "0" sep "return" sep НомерВершины endl.
НомерВершины = Положительное.
НомерЗадержанногоСписка = Положительное.
Оператор = Операция {/ sep Ссылка /}.
Позиции = "pos" sep Положительное sep Положительное sep Положительное sep Положительное.
Ссылка = ( [ "ext:" | "loc:" ] Положительное ) | Элементарное .
Операция = ":" | "(---)" | "[---]" | "<(--)".
Целое = ["+" | "-"] Положительное.
Положительное = {/ цифра /}
Действительное = ["+" | "-"] Положительное "." Положительное ["E" [ "+" | "-" ] Положительное].
Задержанное = "{" НомерЗадержанногоСписка "}" sep НомерВершины.
Элементарное = "." | "+" | "-" | "*" | "/" | "%" | ">" | "<" | "=" | "<=" | ">=" | "<>" | "?" | "|" | … .
Цифра = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9".
Буква = "A" | … | "Z" | "a" | … | "z".

=========================================================================================================

УГ = Название Граф Сигналы ДинамическиеСвязи.
Название = id ["." id].
Граф = ЗаголовокГрафа СписокВершин.
Сигналы = ЗаголовокСигналов СписокСигналов.
ДинамическиеСвязи = ЗаголовокДинамическихСвязей [СписокДинамическихСвязей].
ЗаголовокГрафа = "id" sep "delay" sep "automat" sep "inode" sep "links".
СписокВершин = {/ Вершина /}.
ЗаголовокСигналов = "Signals: (Number, Node, Input)".
СписокСигналов = {/ Сигнал /}.
ЗаголовокДинамическихСвязей = "Dynamic links: (Number, Delay list, Node)".
СписокДинамическихСвязей = {/ ДинамическаяСвязь /}.
Вершина = Номер sep НомерЗадержанногоСписка sep Автомат sep НомерУзлаРИГ [sep СписокСвязей].
Сигнал = Номер sep ПриемникСигнала sep ВходПриемника.
ДинамическаяСвязь = Номер sep НомерЗадержанногоСписка sep КореньСписка.
Номер = Положительное.
НомерЗадержанногоСписка = Положительное.
Автомат = ТипАвтомата "," СостояниеАвтомата.
НомерУзлаРИГ = Положительное.
СписокСвязей = "links:" Связь {";"Связь}.
Связь = ПриемникСигнала "," ВходПриемника.
ПриемникСигнала = Положительное.
ВходПриемника = Положительное.
КореньСписка = Положительное.
ТипАвтомата = "arg" | "(---)" | "[---]" | "<(--)" | ":" | "return".
СостояниеАвтомата = Положительное.
Положительное = {/ цифра /}
sep = {/ tab | space /}.
id = ( буква | "_" ) { буква | цифра | "_" }.
цифра = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9".
буква = "A" | … | "Z" | "a" | … | "z".